
pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        maven { url 'https://jitpack.io'}
    }
}
dependencyResolutionManagement {

    Properties properties = new Properties()
    def localPropertiesPath = 'local.properties'
    if (file(localPropertiesPath).exists()) {
        properties.load(file(localPropertiesPath).newDataInputStream())
    }

    gradle.ext {
        nexusUrl = properties.getProperty("nexusUrl") ?: ""
        nexusUsername = properties.getProperty("nexusUsername")
        nexusPassword = properties.getProperty("nexusPassword")
        gitHash = { ->
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        }
    }

    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io'}
        maven { url gradle.nexusUrl }
    }
}

include ':app', ':core', ':processor', ':annotations', ':crash', ':stub', ':mvp-processor'
